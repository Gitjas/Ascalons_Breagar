// this array can be pre-loaded with items that 
// 1) Braegar shouldn't be able to wield with one hand 
// 2) and come from mods which could be installed *after* Braegar
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_2h_breagar BEGIN 
  // keep the item names in lowercase
  ~c2bow01~  => 0 // [item upgrade] teleomortis +4
  ~c2sper01~ => 0 // [item upgrade] throwing spear +3
  ~c2sper02~ => 0 // [item upgrade] throwing unicorn spear +3
  ~c2sper03~ => 0 // [item upgrade] throwing impaler +4
  ~c2staf01~ => 0 // [item upgrade] staff of elemental mastery
  ~c2sw2h01~ => 0 // [item upgrade] lilarcor +5
  ~cdjoorg~  => 0 // [sp collection] joorg's demise
  ~u#ax2h85~ => 0 // [under-represented items] holy greataxe of justice +5
  ~u#ax2h86~ => 0 // [under-represented items] greataxe of ice +5
  ~u#ax2h87~ => 0 // [under-represented items] greataxe of blurred visions +5
  ~u#ax2h93~ => 0 // [under-represented items] greataxe of digestion +5
  ~u#ax2h94~ => 0 // [under-represented items] massive greataxe of flame +5
  ~u#ax2h95~ => 0 // [under-represented items] greataxe of might +5
  ~u#ax2h98~ => 0 // [under-represented items] greataxe of elemental bursts
  ~u#ax2h99~ => 0 // [under-represented items] greataxe of vampiric flame
  ~u#fhsrim~ => 0 // [under-represented items] life's blood drinker
  ~u#halb95~ => 0 // [under-represented items] infernal halberd of the defender
  ~u#halb97~ => 0 // [under-represented items] halberd of speed +5
  ~u#hfhbhl~ => 0 // [under-represented items] holy swizarnian hammer of lucerne
  ~u#hfhbpb~ => 0 // [under-represented items] pudu's fiery blight
  ~u#hfsrnp~ => 0 // [under-represented items] twelve paces
  ~u#sper89~ => 0 // [under-represented items] spear of stunning +2
  ~u#staf86~ => 0 // [under-represented items] phantom staff +5
  ~u#staf90~ => 0 // [under-represented items] staff of greater spell resistance +5
END

// now, check items already present and add to array if one-handed braegar can, but shouldn't, be able to wield them
// if item already present in pre-load array, it just replaces the old entry so no dupe 180s
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_LONG 0x18 flags_itm // general item flags
  READ_LONG 0x1e flags_use // usability flags
  PATCH_IF (((flags_itm & BIT1) = BIT1) AND  // if item is two-handed
            ((flags_itm & BIT2) = BIT2) AND  // and droppable
            ((flags_use & BIT2) = 0) AND     // and usable by lawful
            ((flags_use & BIT4) = 0) AND     // and usable by good 
            ((flags_use & BIT11) = 0) AND    // and usable by fighter
            ((flags_use & BIT24) = 0)) BEGIN // and usable by dwarf 
    TO_LOWER SOURCE_RES
    DEFINE_ASSOCIATIVE_ARRAY cd_2h_breagar BEGIN ~%SOURCE_RES%~ => 0 END // add to array
  END
  BUT_ONLY    

// now add 180s to the ring/stump to prevent this items from being wielded
OUTER_SET breagar_2h_objection = RESOLVE_STR_REF (@216)  
COPY_EXISTING ~achand1a.itm~ ~override~
  PATCH_PHP_EACH cd_2h_breagar AS item => foo BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 180 target = 1 parameter1 = breagar_2h_objection timing = 2 STR_VAR resource = EVAL ~%item%~ END
  END
  BUT_ONLY 