/* Skript SoD */

/* after scene in Ducal Palace - Breagar will come to PC */

IF
	%SoD_VAR%
	!InParty(Myself)
	Global("ACBREINPARTY","GLOBAL",3)
	AreaCheck("bd0103")
	OR(2)
		!Range(Player1,15)
		!See(Player1)
	Global("bd_001_plot","bd0103",9)
	Global("ACBRE_SoDJoin","LOCALS",0)
	
THEN
	RESPONSE #100
		SetGlobal("ACBRE_SoDJoin","LOCALS",1)
		MoveToObject(Player1)
END

IF
	%SoD_VAR%
	!InParty(Myself)
	Global("ACBREINPARTY","GLOBAL",3)
	AreaCheck("bd0103")
	Range(Player1,15)
	See(Player1)
	GlobalLT("ACBRE_SoDJoin","LOCALS",2)
	
THEN
	RESPONSE #100
		SetGlobal("ACBRE_SoDJoin","LOCALS",2)
		StartDialogNoSet(Player1)
END


/* talk to PC if kicked out */
IF
	%SoD_VAR%
	!InParty(Myself)
	Global("ACBREINPARTY","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* Move camp if kicked out and waiting */

IF
	%SoD_VAR%
	Global("bd_npc_camp","locals",1)
	Switch("bd_npc_camp_chapter","global")
	OR(2)
		!Range("ff_camp",999)
		!TriggerOverride("ff_camp",IsOverMe("ACBre"))  
THEN
	RESPONSE #2
		EscapeAreaMove("bd1000",300,3435,N) 
	RESPONSE #3
		EscapeAreaMove("bd7100",1000,3595,N) 
	RESPONSE #4
		EscapeAreaMove("bd3000",469,570,N) 
END

IF
	%SoD_VAR%
	GlobalLT("bd_npc_camp","locals",2)
	Global("ACBREINPARTY","GLOBAL",3)
	TriggerOverride("ff_camp",IsOverMe("ACBre"))  
	Switch("bd_npc_camp_chapter","global")
THEN
	RESPONSE #2
		SetGlobal("bd_npc_camp","locals",2)
		SaveLocation("LOCALS","bd_default_loc",[300.3435])
	RESPONSE #3
		SetGlobal("bd_npc_camp","locals",2)
		SaveLocation("LOCALS","bd_default_loc",[1000.3595])
	RESPONSE #4
		SetGlobal("bd_npc_camp","locals",2)
		SaveLocation("LOCALS","bd_default_loc",[469.570])
		ChangeAIScript("bdasc3",CLASS)
END

/* SoD gameplay */

/* Breagar comments on deal with Lich */
IF
	%SoD_VAR%
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2)
		Global("BD_DOD_COLDH_CONSIDER","LOCALS",1)
		Global("BD_DOD_KILL_CLERICS","GLOBAL",1)
	!Dead("BDDEEP")
	!Dead("BDDEEP2")
	Global("ACBre_BDCOLDH_17","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("ACBre_BDCOLDH_17","GLOBAL",2)
END

IF
	%SoD_VAR%
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("ACBre_BDCOLDH_17","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* Breagar turns on CHARNAME if Dwarves are attacked */

IF 
	OR(4)
		Allegiance("BDDEEP",[ENEMY])
		Allegiance("BDDEEP2",[ENEMY])
		Dead("BDDEEP")
		Dead("BDDEEP2")
	OR(2)
		See("BDDEEP")
		See("BDDEEP2")
	Global("ACBre_SoDDeepvein","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("ACBre_SoDDeepvein","LOCALS",1)
END

IF
	%SoD_VAR%
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("ACBre_SoDDeepvein","LOCALS",1)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* Breagar reacts to Thrix's game */

/* PC offered Breagar's soul */
IF
	%SoD_VAR%
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("ACBre_SoDThrix","GLOBAL",2)
	GlobalLT("bd_plot","global",550)
	OR(2)
	Global("bd_thrix_plot","global",20)
THEN
	RESPONSE #100
		SetGlobal("ACBre_SoDThrix","GLOBAL",3)
END

/* PC offered Breagar's soul */
IF
	%SoD_VAR%
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	GlobalLT("ACBre_SoDThrix","GLOBAL",2)
	Global("bd_thrix_sacrifice_companion","global",1)
	GlobalLT("bd_plot","global",550)
	Global("bd_thrix_plot","global",20)
THEN
	RESPONSE #100
		SetGlobal("ACBre_SoDThrix","GLOBAL",5)
END

IF
	%SoD_VAR%
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2)
		Global("ACBre_SoDThrix","GLOBAL",3)
		Global("ACBre_SoDThrix","GLOBAL",5)		
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

